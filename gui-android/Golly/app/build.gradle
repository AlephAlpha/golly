apply plugin: "com.android.application"

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.2"

    defaultConfig {
        applicationId "net.sf.golly"
        minSdkVersion 11
        targetSdkVersion 17
        versionCode 3
        versionName "1.2"
        ndk {
            abiFilters "armeabi-v7a", "armeabi"
        }
        externalNativeBuild {
            ndkBuild {
                arguments "NDK_APPLICATION_MK:=Application.mk"
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildTypes {
        debug {
            ndk {
                debuggable true
            }
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt")
            ndk {
                debuggable true
            }
        }
    }

    externalNativeBuild {
        ndkBuild {
            path "Android.mk"
        }
    }
}

dependencies {
    compile "com.android.support:support-v4:24.2.1"
}

task zipHelp(type: Zip) {
    archiveName "Help.zip"
    from "../../../gui-common/Help"
    include "**/*"
    into "Help"
    destinationDir file("src/main/assets/")
}

task zipPatterns(type: Zip) {
    archiveName "Patterns.zip"
    from "../../../Patterns"
    include "**/*"
    into "Patterns"
    destinationDir file("src/main/assets/")
}

task zipRules(type: Zip) {
    archiveName "Rules.zip"
    from "../../../Rules"
    include "**/*"
    into "Rules"
    destinationDir file("src/main/assets/")
}

preBuild.dependsOn(zipHelp)
preBuild.dependsOn(zipPatterns)
preBuild.dependsOn(zipRules)
